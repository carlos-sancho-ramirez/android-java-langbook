apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

group = 'sword'
version = '1.0'

sourceCompatibility = "8"
targetCompatibility = "8"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.carlos-sancho-ramirez:lib-java-collections:413abc35144ca3770e697a020849db21aa5a8382'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'bit-streams-library'
            from components.java

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor false

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}
